#version 460

#define COMPUTE_SHADER
#include "majercik2019.glsl"



uint getProbeID(ivec2 texelCoord) {
	uint probeSampleSideLengthWBorder = renderSettings.probeSampleSideLength + 2;
	uint singleExtend = 2 * renderSettings.singleDirectionProbeCount + 1;
	uint probeCountSideX = singleExtend * singleExtend;
	return uint(texelCoord.x / probeSampleSideLengthWBorder) + probeCountSideX * uint(texelCoord.y / probeSampleSideLengthWBorder);
}

vec2 normalizedOctCoord(ivec2 texelCoord) {
	uint probeSampleSideLengthWBorder = renderSettings.probeSampleSideLength + 2;

	ivec2 octFragCoord = ivec2((texelCoord.x - 2) % probeSampleSideLengthWBorder, (texelCoord.y - 2) % probeSampleSideLengthWBorder);

	return (vec2(octFragCoord) + vec2(0.5)) * (2.0 / float(renderSettings.probeSampleSideLength)) - vec2(1.0);
}


void main() {
	ivec2 bufferPos = ivec2(gl_GlobalInvocationID.xy);
	ivec2 pos = bufferPos;
	ivec2 bufferSize = imageSize(irradianceBuffer);
	int sideLenght = int(renderSettings.probeSampleSideLength);

	if (pos.x == 0 || pos.x == bufferSize.x - 1 || pos.y == 0 || pos.y == bufferSize.y - 1) {
		if (pos.x == 0) {
			pos.x += sideLenght;
		} else if (pos.x == bufferSize.x - 1) {
			pos.x -= sideLenght;
		}

		if (pos.y == 0) {
			pos.y += sideLenght;
		} else if (pos.y == bufferSize.y - 1) {
			pos.y -= sideLenght;
		}
	} else {
		ivec2 relPos = (pos - ivec2(1)) % ivec2(sideLenght + 2);
		
		if (relPos.x == 0) {
			pos.x += 1;
		} else if (relPos.x == sideLenght + 1) {
			pos.x -= 1;
		}

		if (relPos.y == 0) {
			pos.y += 1;
		} else if (relPos.y == sideLenght + 1) {
			pos.y -= 1;
		}
	}

	uint probeID = getProbeID(pos);

	vec3 irradianceResult = vec3(0.0);
	float totalIrradianceWeight = 0.0;
	vec2 dephtResult = vec2(0.0);
	float totalDepthWeight = 0.0;

	vec3 texelDirection = octDecode(normalizedOctCoord(pos));

	for (uint i = 0; i < renderSettings.perProbeRayCount; i++) {
		uint surfelIndex = probeID * renderSettings.perProbeRayCount + i;

		vec3 rayDirection = surfels.s[surfelIndex].rayDirection;
		vec3 hitRadiance = surfels.s[surfelIndex].hitRadiance;
		float hitDistance = surfels.s[surfelIndex].hitDistance;
		bool hit = surfels.s[surfelIndex].hit;

		float rayProbeDistance = renderSettings.maxProbeRayDistance;
		if (hit) rayProbeDistance = min(rayProbeDistance, hitDistance);

		float irradianceWeight = max(0.0, dot(texelDirection, rayDirection));
		float depthWeight = pow(irradianceWeight, renderSettings.depthSharpness);

		if (irradianceWeight > 1e-6) {
			irradianceResult += irradianceWeight * hitRadiance;
			totalIrradianceWeight += irradianceWeight;
		}
		
		if (depthWeight > 1e-6) {
			dephtResult += depthWeight * vec2(rayProbeDistance, rayProbeDistance*rayProbeDistance);
			totalDepthWeight += depthWeight;
		}
	}

	imageStore(irradianceBuffer, bufferPos, vec4(irradianceResult / totalIrradianceWeight, 0.0));
	imageStore(depthBuffer, bufferPos, vec4(dephtResult / totalDepthWeight, 0.0, 0.0));
}
